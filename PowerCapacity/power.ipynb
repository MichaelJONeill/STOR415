{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Power Capacity Planning"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "- A regional energy company is planning for its energy generation mix for the next decade. The generation mix consists of nuclear, coal, and solar energy. The energy company spans two counties, A and B.\n",
    "- The projected maximum demand for the entire region beyond the current grid's capacity is $5.0 \\times 10^3$ MW. We assume that there is no power lost in transmission between the counties (that is, we can satisfy this capacity requirement with any combination of power from county A and B).\n",
    "- The development costs of power capacity are given in the table below in dollars per MW.\n",
    "\n",
    "| Type/County | Nuclear | Coal | Solar |\n",
    "| ----------- | ------- | ---- | ----- |\n",
    "| A | $5.2 \\times 10^6$ | $2.5 \\times 10^6$ | $8.0 \\times 10^6$ |\n",
    "| B | $4.8 \\times 10^6$ | $2.2 \\times 10^6$ | $8.5 \\times 10^6$ |\n",
    "\n",
    "- The greenhouse gas emission rates of the power sources are $1.5\\times 10^3$ tons/MW, $5.3 \\times 10^3$ tons/MW, and $0.1 \\times 10^3$ tons/MW for nuclear, coal, and solar respectively. There is a region-wide ceiling for the total greenhouse gas emissions at $7.2 \\times 10^6$ tons.\n",
    "- County B is particular about power generation and wants no nuclear power generated in county B, no more than $1.5 \\times 10^3$ MW capacity generated by coal in county B, and no less than $2 \\times 10^3$ MW capacity generated by solar in county B.\n",
    "- How much nuclear, coal, and solar energy should we plan to develop to minimize the cost while satisfying all constraints?\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "- Define Types = {Nuclear, Coal, Solar} and Counties = {A, B}.\n",
    "- Define parameters:\n",
    "    - $c_{i,j}$: The cost of power for each $i \\in$ Types and $j \\in$ Counties.\n",
    "    - $e_{i}$: The emissions for each $i \\in$ Types.\n",
    "    - $e_{\\max}$: The emissions ceiling.\n",
    "    - $d_{\\max}$: The maximum demand.\n",
    "    - $\\ell_{i,j},\\ u_{i,j}$: Lower and upper bounds on the added capacity for each $i \\in$ Types and $j \\in$ Counties (may be $\\pm \\infty$)\n",
    "- The optimization problem is:\n",
    "\\begin{align*}\n",
    "\\underset{x}{\\min} \\quad  &\\sum_{i \\in \\text{Types}} \\sum_{j \\in \\text{Counties}} c_{i,j} x_{i,j} \\\\\n",
    "\\text{s.t.} \\ \\ \\quad &\\sum_{i \\in \\text{Types}} \\sum_{j \\in \\text{Counties}} x_{i,j}  \\geq d_{\\max} \\\\\n",
    "&\\sum_{i \\in \\text{Types}} e_i \\sum_{j \\in \\text{Counties}} x_{i,j} \\leq e_{\\max} \\\\\n",
    "&x_{i,j} \\in [\\ell_{i,j}, u_{i,j}].\n",
    "\\end{align*}\n",
    "\n",
    "Let's define the data in code."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "3×2 Named Matrix{Int64}\n",
       "type ╲ county │ A  B\n",
       "──────────────┼─────\n",
       "Nuclear       │ 0  0\n",
       "Coal          │ 0  0\n",
       "Solar         │ 0  2"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "types = [:Nuclear, :Coal, :Solar]\n",
    "counties = [:A, :B]\n",
    "\n",
    "dmax = 5\n",
    "emax = 7.2*10^3\n",
    "\n",
    "using NamedArrays\n",
    "#Remember that the x variables will be in units of 10^3 MW \n",
    "cmat = [5.2*10^3 4.8*10^3; 2.5*10^3 2.25*10^3; 8*10^3 8.5*10^3]\n",
    "c = NamedArray( cmat, (types,counties), (\"type\",\"county\") )\n",
    "\n",
    "e = Dict(zip(types,[1.5,5.3,0.1]))\n",
    "\n",
    "umat = [Inf 0; Inf 1.5; Inf Inf]\n",
    "u = NamedArray( umat, (types,counties), (\"type\",\"county\") )\n",
    "\n",
    "lmat = [0 0; 0 0; 0 2]\n",
    "l = NamedArray( lmat, (types,counties), (\"type\",\"county\") )\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now, let's define the model."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Min 5200 x[Nuclear,A] + 4800 x[Nuclear,B] + 2500 x[Coal,A] + 2250 x[Coal,B] + 8000 x[Solar,A] + 8500 x[Solar,B]\n",
      "Subject to\n",
      " "
     ]
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "demand : x[Nuclear,A] + x[Coal,A] + x[Solar,A] + x[Nuclear,B] + x[Coal,B] + x[Solar,B] ≥ 5\n",
      " emissions : 1.5 x[Nuclear,A] + 5.3 x[Coal,A] + 0.1 x[Solar,A] + 1.5 x[Nuclear,B] + 5.3 x[Coal,B] + 0.1 x[Solar,B] ≤ 7200\n",
      " x[Nuclear,A] ≥ 0\n",
      " x[Coal,A] ≥ 0\n",
      " x[Solar,A] ≥ 0\n",
      " x[Nuclear,B] ≥ 0\n",
      " x[Coal,B] ≥ 0\n",
      " x[Solar,B] ≥ 2\n",
      " x[Nuclear,B] ≤ 0\n",
      " x[Coal,B] ≤ 1.5\n"
     ]
    }
   ],
   "source": [
    "using JuMP\n",
    "using HiGHS\n",
    "\n",
    "power = Model(HiGHS.Optimizer)\n",
    "\n",
    "@variable(power, l[i,j] <= x[i in types, j in counties] <= u[i,j])\n",
    "\n",
    "@constraint(power, demand, sum(sum(x[i,j] for j in counties) for i in types) >= dmax)\n",
    "@constraint(power, emissions, sum(e[i]*sum(x[i,j] for j in counties) for i in types) <= emax)\n",
    "\n",
    "@objective(power, Min, sum(sum(c[i,j]*x[i,j] for j in counties) for i in types))\n",
    "\n",
    "print(power)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Running HiGHS 1.6.0: Copyright (c) 2023 HiGHS under MIT licence terms\n",
      "Presolving model\n",
      "2 rows, 5 cols, 10 nonzeros\n",
      "2 rows, 4 cols, 8 nonzeros\n",
      "Presolve : Reductions: rows 2(-0); columns 4(-2); elements 8(-4)\n",
      "Solving the presolved LP\n",
      "Using EKK dual simplex solver - serial\n",
      "  Iteration        Objective     Infeasibilities num(sum)\n",
      "          0     1.7000000000e+04 Pr: 1(3) 0s\n",
      "          1     2.4125000000e+04 Pr: 0(0) 0s\n",
      "Solving the original LP from the solution after postsolve\n",
      "Model   status      : Optimal\n",
      "Simplex   iterations: 1\n",
      "Objective value     :  2.4125000000e+04\n",
      "HiGHS run time      :          0.00\n"
     ]
    }
   ],
   "source": [
    "optimize!(power)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "objective_value(power) = 24125.0\n",
      "value.(x) = 2-dimensional DenseAxisArray{Float64,2,...} with index sets:\n",
      "    Dimension 1, [:Nuclear, :Coal, :Solar]\n",
      "    Dimension 2, [:A, :B]\n",
      "And data, a 3×2 Matrix{Float64}:\n",
      " 0.0  0.0\n",
      " 1.5  1.5\n",
      " 0.0  2.0\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "2-dimensional DenseAxisArray{Float64,2,...} with index sets:\n",
       "    Dimension 1, [:Nuclear, :Coal, :Solar]\n",
       "    Dimension 2, [:A, :B]\n",
       "And data, a 3×2 Matrix{Float64}:\n",
       " 0.0  0.0\n",
       " 1.5  1.5\n",
       " 0.0  2.0"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "@show objective_value(power);\n",
    "@show value.(x);"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's look at how sensitive our solution is to the data."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "SensitivityReport{Float64}(Dict{ConstraintRef, Tuple{Float64, Float64}}(x[Coal,B] ≤ 1.5 => (-1.5, 1.5), x[Solar,A] ≥ 0 => (-1381.5192307692307, 1.5), x[Nuclear,B] ≥ 0 => (-1890.5000000000002, 0.0), emissions : 1.5 x[Nuclear,A] + 5.3 x[Coal,A] + 0.1 x[Solar,A] + 1.5 x[Nuclear,B] + 5.3 x[Coal,B] + 0.1 x[Solar,B] ≤ 7200 => (-7183.9, Inf), x[Solar,B] ≥ 2 => (-1381.5192307692307, 1.5), x[Coal,A] ≥ 0 => (-Inf, 1.5), x[Nuclear,B] ≤ 0 => (0.0, Inf), demand : x[Nuclear,A] + x[Coal,A] + x[Solar,A] + x[Nuclear,B] + x[Coal,B] + x[Solar,B] ≥ 5 => (-1.5, 1355.4528301886792), x[Coal,B] ≥ 0 => (-Inf, 1.5), x[Nuclear,A] ≥ 0 => (-1890.5000000000002, 1.5)…), Dict{VariableRef, Tuple{Float64, Float64}}(x[Solar,A] => (-5500.0, Inf), x[Solar,B] => (-6000.0, Inf), x[Nuclear,B] => (-Inf, Inf), x[Coal,B] => (-Inf, 250.0), x[Coal,A] => (-250.0, 2700.0), x[Nuclear,A] => (-2700.0, Inf)))"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "report = lp_sensitivity_report(power)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's see how much the coefficient for cost of adding capacity with nuclear for county A can change before the solution changes."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "c[:Nuclear,:A] can stay between 2500.0 and Inf\n"
     ]
    }
   ],
   "source": [
    "NArange = report[x[:Nuclear,:A]]\n",
    "println(\"c[:Nuclear,:A] can stay between \", c[:Nuclear,:A]+NArange[1], \" and \", c[:Nuclear,:A]+NArange[2])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "What about for county B?"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "c[:Nuclear,:B] can stay between -Inf and Inf\n"
     ]
    }
   ],
   "source": [
    "NBrange = report[x[:Nuclear,:B]]\n",
    "println(\"c[:Nuclear,:B] can stay between \", c[:Nuclear,:B]+NBrange[1], \" and \", c[:Nuclear,:B]+NBrange[2])"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Remember, county B does not want any nuclear power capacity added, so the value of c does not change the solution!\n",
    "\n",
    "Let's try looking at what happens when we change the coefficient of the emissions constraint."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "emax can stay between 16.100000000000364 and Inf\n"
     ]
    }
   ],
   "source": [
    "Emissionsrange = report[emissions]\n",
    "println(\"emax can stay between \", emax+Emissionsrange[1], \" and \", emax+Emissionsrange[2])"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.3",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.3"
  },
  "orig_nbformat": 4
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
