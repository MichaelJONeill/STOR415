{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# Traveling salesman problem\n",
    "\n",
    "All code for this example is based on code by Laurent Lessard: https://laurentlessard.com.\n",
    "\n",
    "Find the closed tour that visits all ten cities and has the minimum total length. Distances between pairs of cities are given in the table below:\n",
    "\n",
    "|         |  ATL  |  ORD  |  DEN  |  IAH  |  LAX  |  MIA  |  JFK  |  SFO  |  SEA  |  DCA  |\n",
    "|--------:|------:|------:|------:|------:|------:|------:|------:|------:|------:|------:|\n",
    "|**ATL**  |    0  |  587  | 1212  |  701  | 1936  |  604  |  748  | 2139  | 2182  |  543  |\n",
    "|**ORD**  |  587  |    0  |  920  |  940  | 1745  | 1188  |  713  | 1858  | 1737  |  597  |\n",
    "|**DEN**  | 1212  |  920  |    0  |  879  |  831  | 1726  | 1631  |  949  | 1021  | 1494  |\n",
    "|**IAH**  |  701  |  940  |  879  |    0  | 1379  |  968  | 1420  | 1645  | 1891  | 1220  |\n",
    "|**LAX**  | 1936  | 1745  |  831  | 1379  |    0  | 2339  | 2451  |  347  |  959  | 2300  |\n",
    "|**MIA**  |  604  | 1188  | 1726  |  968  | 2339  |    0  | 1092  | 2594  | 2734  |  923  |\n",
    "|**JFK**  |  748  |  713  | 1631  | 1420  | 2451  | 1092  |    0  | 2571  | 2408  |  205  |\n",
    "|**SFO**  | 2139  | 1858  |  949  | 1645  |  347  | 2594  | 2571  |    0  |  678  | 2442  |\n",
    "|**SEA**  | 2182  | 1737  | 1021  | 1891  |  959  | 2734  | 2408  |  678  |    0  | 2329  |\n",
    "|**DCA**  |  543  |  597  | 1494  | 1220  | 2300  |  923  |  205  | 2442  | 2329  |    0  |"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define the problem data (cities and costs)\n",
    "# Run this, then run the final block of this notebook to load the helper functions before continuing\n",
    "using JuMP, NamedArrays, HiGHS\n",
    "\n",
    "cities = [:ATL, :ORD, :DEN, :IAH, :LAX, :MIA, :JFK, :SFO, :SEA, :DCA]\n",
    "\n",
    "distances = [     0   587  1212   701  1936   604   748  2139  2182   543\n",
    "                587     0   920   940  1745  1188   713  1858  1737   597\n",
    "               1212   920     0   879   831  1726  1631   949  1021  1494\n",
    "                701   940   879     0  1379   968  1420  1645  1891  1220\n",
    "               1936  1745   831  1379     0  2339  2451   347   959  2300\n",
    "                604  1188  1726   968  2339     0  1092  2594  2734   923\n",
    "                748   713  1631  1420  2451  1092     0  2571  2408   205\n",
    "               2139  1858   949  1645   347  2594  2571     0   678  2442\n",
    "               2182  1737  1021  1891   959  2734  2408   678     0  2329\n",
    "                543   597  1494  1220  2300   923   205  2442  2329     0  ]\n",
    "\n",
    "c = NamedArray(distances,(cities,cities))\n",
    "N = size(distances,1);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Run the final block of this notebook to load helper functions before continuing!\n",
    "mapSolution(0)\n",
    "xlabel(\"plot showing the 10 airport locations\");\n",
    "savefig(\"tsp_initial.pdf\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solve TSP using min-cost flow relaxation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# solve the simplest version (min-cost flow version; it's an assignment problem)\n",
    "#using Pkg\n",
    "#Pkg.add(\"Cbc\")\n",
    "\n",
    "m = Model(HiGHS.Optimizer)\n",
    "@variable(m, 0 <= x[cities,cities] <= 1)                                 # LP relaxation\n",
    "@constraint(m, c1[j in cities], sum( x[i,j] for i in cities ) == 1)    # exacly one edge out of each node\n",
    "@constraint(m, c2[i in cities], sum( x[i,j] for j in cities ) == 1)    # exactly one edge into each node\n",
    "@constraint(m, c3[i in cities], x[i,i] == 0 )                       # no self-loops\n",
    "@objective(m, Min, sum( x[i,j]*c[i,j] for i in cities, j in cities ))  # minimize total cost\n",
    "optimize!(m)\n",
    "\n",
    "# pretty print the solution\n",
    "xx = value.(x)\n",
    "sol = NamedArray(zeros(Int,N,N),(cities,cities))\n",
    "for i in cities\n",
    "    for j in cities\n",
    "        sol[i,j] = Int(xx[i,j])\n",
    "    end\n",
    "end\n",
    "println(sol)\n",
    "sleep(1)\n",
    "display(getAllSubtours(sol))\n",
    "mapSolution(sol)\n",
    "xlabel(\"looks like the relaxation isn't what we wanted!\")\n",
    "tight_layout()\n",
    "savefig(\"tsp_sol_LP.pdf\")\n",
    ";"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Solve TSP using adaptive subtour elimination"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "5-element Vector{Any}:\n",
       " [:ATL, :MIA, :ATL]\n",
       " [:ORD, :IAH, :ORD]\n",
       " [:DEN, :SEA, :DEN]\n",
       " [:LAX, :SFO, :LAX]\n",
       " [:JFK, :DCA, :JFK]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Running HiGHS 1.6.0: Copyright (c) 2023 HiGHS under MIT licence terms\n",
      "Presolving model\n",
      "20 rows, 90 cols, 180 nonzeros\n",
      "20 rows, 90 cols, 180 nonzeros\n",
      "Objective function is integral with scale 1\n",
      "\n",
      "Solving MIP model with:\n",
      "   20 rows\n",
      "   90 cols (90 binary, 0 integer, 0 implied int., 0 continuous)\n",
      "   180 nonzeros\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "     Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   0               inf                  inf        0      0      0         0     0.0s\n",
      " T       0       0         0   0.00%   0               6234             100.00%        0      0      0        21     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      6234\n",
      "  Dual bound        6234\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  Solution status   feasible\n",
      "                    6234 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00 (total)\n",
      "                    0.00 (presolve)\n",
      "                    0.00 (postsolve)\n",
      "  Nodes             1\n",
      "  LP iterations     21 (total)\n",
      "                    0 (strong br.)\n",
      "                    0 (separation)\n",
      "                    0 (heuristics)\n",
      "Tour length: 6234.0\n",
      "Presolving model\n",
      "30 rows, 90 cols, 200 nonzeros\n",
      "25 rows, 90 cols, 190 nonzeros\n",
      "Objective function is integral with scale 1\n",
      "\n",
      "Solving MIP model with:\n",
      "   25 rows\n",
      "   90 cols (90 binary, 0 integer, 0 implied int., 0 continuous)\n",
      "   190 nonzeros\n",
      "\n",
      "        Nodes      |    B&B Tree     |            Objective Bounds              |  Dynamic Constraints |       Work      \n",
      "     Proc. InQueue |  Leaves   Expl. | BestBound       BestSol              Gap |   Cuts   InLp Confl. | LpIters     Time\n",
      "\n",
      "         0       0         0   0.00%   0               inf                  inf        0      0      0         0     0.0s\n",
      " R       0       0         0   0.00%   6663            9327              28.56%        0      0      0        27     0.0s\n",
      " C       0       0         0   0.00%   6665            7948              16.14%       38      3      0        28     0.0s\n",
      " T       0       0         0   0.00%   6665            6665               0.00%       38      3      0        28     0.0s\n",
      "\n",
      "Solving report\n",
      "  Status            Optimal\n",
      "  Primal bound      6665\n",
      "  Dual bound        6665\n",
      "  Gap               0% (tolerance: 0.01%)\n",
      "  Solution status   feasible\n",
      "                    6665 (objective)\n",
      "                    0 (bound viol.)\n",
      "                    0 (int. viol.)\n",
      "                    0 (row viol.)\n",
      "  Timing            0.00 (total)\n",
      "                    0.00 (presolve)\n",
      "                    0.00 (postsolve)\n",
      "  Nodes             1\n",
      "  LP iterations     28 (total)\n",
      "                    0 (strong br.)\n",
      "                    1 (separation)\n",
      "                    0 (heuristics)\n",
      "Tour length: 6665.0\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "3-element Vector{Any}:\n",
       " [:ATL, :IAH, :MIA, :ATL]\n",
       " [:ORD, :DCA, :JFK, :ORD]\n",
       " [:DEN, :LAX, :SFO, :SEA, :DEN]"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "m = Model(HiGHS.Optimizer)\n",
    "@variable(m, x[cities,cities], Bin)                                       # must formulate as IP this time\n",
    "@constraint(m, c1[j in cities], sum( x[i,j] for i in cities ) == 1)     # one out-edge\n",
    "@constraint(m, c2[i in cities], sum( x[i,j] for j in cities ) == 1)     # one in-edge\n",
    "@constraint(m, c3[i in cities], x[i,i] == 0 )                        # no self-loops\n",
    "@objective(m, Min, sum( x[i,j]*c[i,j] for i in cities, j in cities ))   # minimize total cost\n",
    "\n",
    "sols = []\n",
    "\n",
    "for iters = 1:30\n",
    "    optimize!(m)\n",
    "    println(\"Tour length: \", objective_value(m))\n",
    "    xx = value.(x)\n",
    "    push!(sols,xx)\n",
    "    subtours = getAllSubtours(xx)  # get all the subtours\n",
    "    display(subtours)\n",
    "    sleep(1)\n",
    "    len = length(subtours)\n",
    "    if len == 1                    # solution is just a single tour!\n",
    "        println(\"SOLVED!\")\n",
    "        break\n",
    "    else\n",
    "        for subtour in subtours\n",
    "            L = length(subtour)\n",
    "            @constraint(m, sum( x[subtour[k+1],subtour[k]] for k = 1:L-1 ) <= L-2)\n",
    "            @constraint(m, sum( x[subtour[k],subtour[k+1]] for k = 1:L-1 ) <= L-2)\n",
    "        end\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# plot the iterations as we eliminate subtours\n",
    "figure(figsize=(13,6))\n",
    "for i = 1:6\n",
    "    subplot(2,3,i)\n",
    "    mapSolution(sols[i])\n",
    "    xlabel(string(\"iteration \",i))\n",
    "end\n",
    "tight_layout()\n",
    "savefig(\"tsp_sol_elim.pdf\")"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Miller-Tucker-Zemlin formulation"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "m = Model(HiGHS.Optimizer)\n",
    "@variable(m, x[cities,cities], Bin)                                      # must formulate as IP this time\n",
    "@constraint(m, c1[j in cities], sum( x[i,j] for i in cities ) == 1)      # one out-edge\n",
    "@constraint(m, c2[i in cities], sum( x[i,j] for j in cities ) == 1)      # one in-edge\n",
    "@constraint(m, c3[i in cities], x[i,i] == 0 )                            # no self-loops\n",
    "@objective(m, Min, sum( x[i,j]*c[i,j] for i in cities, j in cities ))   # minimize total cost\n",
    "                                    \n",
    "# MTZ variables and constraints\n",
    "@variable(m, u[cities])\n",
    "@constraint(m, c4[i in cities, j in cities[2:end]], u[i] - u[j] + N*x[i,j] <= N-1 )\n",
    "\n",
    "optimize!(m)\n",
    "xx = value.(x)\n",
    "subtours = getAllSubtours(xx)  # get cycle containing Atlanta\n",
    "display(subtours)\n",
    "println(\"Tour length: \", objective_value(m))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "mapSolution(xx)"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Helper functions"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "#Run this to install everything the first time through!!!\n",
    "#Will fail if you do not install everything first!\n",
    "#using Pkg;\n",
    "#Pkg.add(\"PyPlot\")\n",
    "#Pkg.add(\"PythonCall\")\n",
    "#Pkg.add(\"Conda\")\n",
    "#using CondaPkg\n",
    "#CondaPkg.add(\"basemap\")\n",
    "\n",
    "# HELPER FUNCTION: returns the cycle containing the city START.\n",
    "function getSubtour(x,start)\n",
    "    subtour = [start]\n",
    "    while true\n",
    "        j = subtour[end]\n",
    "        for k in cities\n",
    "            if x[k,j] == 1\n",
    "                push!(subtour,k)\n",
    "                break\n",
    "            end\n",
    "        end\n",
    "        if subtour[end] == start\n",
    "            break\n",
    "        end\n",
    "    end\n",
    "    return subtour\n",
    "end\n",
    "\n",
    "# HELPER FUNCTION: returns a list of all cycles\n",
    "function getAllSubtours(x)\n",
    "    nodesRemaining = cities\n",
    "    subtours = []\n",
    "    while length(nodesRemaining) > 0\n",
    "        subtour = getSubtour(x,nodesRemaining[1])\n",
    "        push!(subtours, subtour)\n",
    "        nodesRemaining = setdiff(nodesRemaining,subtour)\n",
    "    end\n",
    "    return subtours\n",
    "end\n",
    "\n",
    "# HELPER FUNCTION FOR MAPPING AIRPORTS\n",
    "\n",
    "data = [ 33.636700  -84.427863 \n",
    "         41.977320  -87.908005\n",
    "         39.861667 -104.673166\n",
    "         29.645417  -95.278888\n",
    "         33.942437 -118.408121\n",
    "         25.795361  -80.290110\n",
    "         40.639926  -73.778694\n",
    "         37.618806 -122.375416\n",
    "         47.449889 -122.311777\n",
    "         38.851444  -77.037721 ]\n",
    "lat = Dict(zip(cities,data[:,1]))\n",
    "lon = Dict(zip(cities,data[:,2]))\n",
    "\n",
    "using PyPlot, PyCall\n",
    "basemap = pyimport(\"mpl_toolkits.basemap\")\n",
    "\n",
    "function mapSolution(x=0)\n",
    "    m=basemap.Basemap(projection=\"merc\", resolution=\"l\",llcrnrlat=23,llcrnrlon=-126,urcrnrlat=50,urcrnrlon=-70)\n",
    "    m[:drawmapboundary](fill_color=\"#4771a5\")\n",
    "    m[:fillcontinents](color=\"#555555\")\n",
    "\n",
    "    # plot airports\n",
    "    for i in cities\n",
    "        m[:plot](lon[i], lat[i], \"ro\" ,latlon=true)\n",
    "    end\n",
    "\n",
    "    # plot tours\n",
    "    if x==0\n",
    "        return\n",
    "    else\n",
    "        tours = getAllSubtours(x)\n",
    "        for t in tours\n",
    "            L = length(t)-1\n",
    "            for i in 1:L\n",
    "                m[:drawgreatcircle](lon[t[i]],lat[t[i]],lon[t[i+1]],lat[t[i+1]],linewidth=1,color=\"b\")\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "end\n",
    ";"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.0",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.0"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
